#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('BuildEnv')
env = BuildEnv.Clone()

base_path = Dir('#src/contrail-api-client/').abspath
sdist_depends = []

# Remove target files, to force scons to build SyncSchema always
clean_yaml_schema = env.Command("rmdir_schema_yaml", None, Delete(base_path + '/schema/yaml'))
sync_yaml_schema = env.SyncSchema([], base_path + '/schema/all_cfg.xsd')
env.Depends(sync_yaml_schema, clean_yaml_schema)
sdist_depends.extend(sync_yaml_schema)

code_gen_rule = env.Command(
    None, base_path + '/code_generate.sh',
    base_path + '/code_generate.sh')
sdist_depends.extend(code_gen_rule)
env.AlwaysBuild(code_gen_rule)

if 'install' in BUILD_TARGETS:
    html_gen_rule = env.Command(
        None, base_path + '/docs_generate.sh',
        base_path + '/docs_generate.sh "' + Dir('%s/contrail-config/doc/' % env['INSTALL_DOC']).abspath + '"')
    env.Depends(html_gen_rule, code_gen_rule)
    sdist_depends.extend(html_gen_rule)
    env.AlwaysBuild(html_gen_rule)
    env.Alias('install', html_gen_rule)

sources = ['setup.py', 'requirements.txt']
sources += env.AddPythonSources('vnc_api', excludes=['gen'])

sdist_gen = env.Command(
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl', sources,
    'cd ' + base_path + '/api-lib' + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
env.Depends(sdist_gen, sdist_depends)
env.Alias('install', sdist_gen)

env.SetupPyTestSuiteWithDeps(sdist_gen, top_dir="#src/contrail-api-client/api-lib")
